apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

def shortHash = 'git log -1 --pretty=%h'.execute().text.trim().toUpperCase()
def commitCont = 'git rev-list HEAD --count'.execute().text.trim().toInteger()
def currentBranch = 'git symbolic-ref --short -q HEAD'.execute().text.trim()
//创建版本号===
//获取之前的版本列表
def tagList = "git describe --tags ${"git rev-list --tags".execute().text.trim()}".execute([], project.rootDir).text.trim().split("\n").toList()
//找到 a.b.c 纯数字版本tag
def lastVersion = tagList.find { tag -> tag.replace(".", "").isNumber() }.split("\\.")
//最后一位 数字 +1
lastVersion[lastVersion.length - 1] = lastVersion[lastVersion.length - 1].toInteger() + 1
def currentVersion = lastVersion.join(".")
android {
    signingConfigs {
        debug {
            storeFile file('../debug.jks')
            storePassword '123456'
            keyAlias = 'debug'
            keyPassword '123456'
        }
    }
    compileSdkVersion COMPILE_SDK_VERSION
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.sjianjun.reader"
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion TARGET_SDK_VERSION
        versionCode commitCont
        versionName "$currentVersion"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "reader-${currentBranch}-${variant.buildType.name}(${variant.versionCode})-${variant.versionName}.apk"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
            manifestPlaceholders = [app_name: "DEBUG"]
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.5'

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'

    def navigationVersion = "2.3.0-alpha06"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"

//    kapt "androidx.lifecycle:lifecycle-compiler:2.2.0"
//    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
//    implementation "androidx.lifecycle:lifecycle-reactivestreams:2.2.0"

    implementation "androidx.room:room-runtime:2.2.5"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    kapt "androidx.room:room-compiler:2.2.5" // For Kotlin use kapt instead of annotationProcessor
    // optional - Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:2.2.5"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation 'com.google.code.gson:gson:2.8.6'

    implementation "org.mozilla:rhino:1.7.12"
    implementation "org.jsoup:jsoup:1.13.1"

    implementation 'com.sjianjun:aLog:1.2.3'
    implementation 'com.sjianjun:simple-http:0.0.3'
    implementation 'com.sjianjun:permission-request:1.1.5'
//    implementation 'com.sjianjun:reflects:0.0.1'

    implementation "org.sufficientlysecure:html-textview:3.9"
    /**
     * 沉浸式
     */
    implementation 'com.gyf.immersionbar:immersionbar:3.0.0'

    implementation 'com.github.razerdp:BasePopup:2.2.2.2'
    //cookie
    implementation "com.github.franmontiel:PersistentCookieJar:v1.0.1"
    implementation "com.github.bumptech.glide:glide:4.11.0"
    kapt 'com.github.bumptech.glide:compiler:4.11.0'
    implementation "com.google.android:flexbox:1.0.0"
    implementation 'com.google.android.material:material:1.2.0-alpha06'

    implementation 'com.tencent.bugly:crashreport:3.2.1'
    implementation "com.victor:lib:1.0.4"

//    implementation project(":refreshbar")
    implementation project(":fastscroller")

    implementation project(":pagerindicator")


}
